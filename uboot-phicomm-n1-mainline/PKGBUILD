# U-Boot: phicomm-n1
# Maintainer: Pu Xiongfei <pu.xiongfei@qq.com>

buildarch=8

pkgname=uboot-phicomm-n1-mainline
pkgver=2022.07
pkgrel=1
pkgdesc="Mainline U-Boot for phicomm-n1"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/boot.txt' 'boot/boot.scr')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "phicomm-n1-${pkgver/rc/-rc}.patch"
        'aml_autoscript.txt'
        'emmc_autoscript.txt'
        's905_autoscript.txt'
        'env_default.txt'
        'boot.txt'
        'mkscr')
md5sums=('b41850031d6e1cbc14f973e085469154'
         '00000000000000000000000000000000'
         'fac80dcdfc4c1cdddec46aa274cd8406'
         '19c9db74781f1499caec2b65fbc26e5a'
         'a497e21f888a571a10d3d111b033ad23'
         '69c51bbf25f190e90254f28bb495dd69'
         '4258258571f02d00d4b38d03b563dc2b'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  git apply ../"phicomm-n1-${pkgver/rc/-rc}".patch
}

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make distclean
  make phicomm-n1_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  install -d "${pkgdir}"/boot
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "aml_autoscript" -d aml_autoscript.txt "${pkgdir}"/boot/aml_autoscript
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "emmc_autoscript" -d emmc_autoscript.txt "${pkgdir}"/boot/emmc_autoscript
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "s905_autoscript" -d s905_autoscript.txt "${pkgdir}"/boot/s905_autoscript
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "env_default" -d env_default.txt "${pkgdir}"/boot/env_default
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d boot.txt "${pkgdir}"/boot/boot.scr
  cp boot.txt mkscr u-boot-${pkgver/rc/-rc}/u-boot.bin "${pkgdir}"/boot
}
