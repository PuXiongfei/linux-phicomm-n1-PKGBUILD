# U-Boot: phicomm-n1
# Maintainer: Pu Xiongfei <pu.xiongfei@qq.com>

buildarch=8

pkgname=uboot-phicomm-n1-mainline
pkgver=2022.10
pkgrel=1
pkgdesc="Mainline U-Boot for phicomm-n1"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
install=$pkgname.install
makedepends=('git' 'bc')
backup=('boot/uEnv.txt')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "meson-gxl-s905d-phicomm-n1-u-boot-${pkgver}.patch"
        'aml_autoscript.txt'
        'env_default.txt'
        'boot.txt'
        'uEnv.txt'
        'mkscr')
md5sums=('cb1f3774f5e423b91cff379101c764a3'
         '67ef7b5a7158223617953faf9345a374'
         '73719c7da590bab1367c57836814ebc6'
         '69c51bbf25f190e90254f28bb495dd69'
         'ffc564748d7b643235f2657e53bb8681'
         'c34bc6cf17fd3eb060299c76e35fc14b'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  patch -p1 -i ../meson-gxl-s905d-phicomm-n1-u-boot-${pkgver}.patch
}

build() {
  cd u-boot-${pkgver/rc/-rc}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  make distclean
  make phicomm-n1_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make -j1 EXTRAVERSION=-${pkgrel}
}

package() {
  install -d "${pkgdir}"/boot
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "aml_autoscript" -d aml_autoscript.txt "${pkgdir}"/boot/aml_autoscript
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "env_default" -d env_default.txt "${pkgdir}"/boot/env_default
  u-boot-${pkgver/rc/-rc}/tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d boot.txt "${pkgdir}"/boot/boot.scr
  cp boot.txt mkscr u-boot-${pkgver/rc/-rc}/u-boot.bin uEnv.txt "${pkgdir}"/boot
}
